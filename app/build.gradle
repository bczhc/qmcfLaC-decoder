import pers.zhc.plugins.BuildUtils
import pers.zhc.plugins.BuildUtils2

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def commitLogResult = null
try {
    def gitVersion = BuildUtils2.checkGitVersion()
    println "Found git $gitVersion"

    try {
        def log = BuildUtils2.getGitCommitLog(projectDir)
        println "git log string size: ${log.bytes.length}"
        if (log.isEmpty()) {
            commitLogResult = "Unknown"
        } else {
            commitLogResult = log
        }
    } catch(e) {
        throw new GradleException("Execution failed", e)
    }
} catch (e) {
    if (e instanceof GradleException) {
        throw e
    }
    println "Found git error: ${e.toString()}; disable git commit log injection"
}

android {
    signingConfigs {
        debug {
            storeFile file('test.jks')
            storePassword '123456'
            keyAlias 'key0'
            keyPassword '123456'
        }
        release {
            storeFile file('test.jks')
            storePassword '123456'
            keyPassword '123456'
            keyAlias 'key0'
        }
    }
    compileSdkVersion 29
    defaultConfig {
        applicationId "pers.zhc.tools"
        minSdkVersion 21
        targetSdkVersion 29
        def verInfo = BuildUtils.gVersion()
        versionCode(verInfo[0] as int)
        versionName(verInfo[1] as String)
        ndk {
            abiFilters "arm64-v8a"
        }
        buildConfigField 'String', 'commitLog', "\"$commitLogResult\""
    }
    buildTypes {
        debug {
            //noinspection GroovyAssignabilityCheck
            minifyEnabled false
            shrinkResources false
            proguardFiles 'proguard-rules-debug.pro'
        }
        release {
            //noinspection GroovyAssignabilityCheck
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            debuggable true
            jniDebuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    def ndkDir
    def sdkDir = android.sdkDirectory
    try {
        ndkDir = android.ndkDirectory
    } catch (def ignored) {
        throw new GradleException("Please define ndk.dir in local.properties")
    }

    def list = [ndkDir, sdkDir]
    def tools = BuildUtils.Tools.newInstance(list as Object[])
    ndkVersion = tools.getNdkVersion()
    println("Ndk Version: ${ndkVersion}")
    externalNativeBuild {
        cmake {
            version tools.getCMakeVersion()
            println("CMake Version: ${getVersion()}")
            path file('src/main/jni/cpp/CMakeLists.txt')
        }
    }
}


repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'org.mariuszgromada.math:MathParser.org-mXparser:4.3.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.4.32"
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.github.mik3y:usb-serial-for-android:3.3.0'
    implementation 'com.github.bczhc:java-lib:18a858c167'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.2'
    implementation 'me.zhanghai.android.fastscroll:library:1.1.7'
    implementation 'com.github.bczhc:jni-java:292ec4a849'
}
